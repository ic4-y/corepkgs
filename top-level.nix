# This a top-level overlay which is applied after this "autoCalled" pkgs directory.
# This mainly serves as a way to define attrs at the top-level of pkgs which
# require more than just passing default arguments to nix expressions

final: prev: with final; {

  # Pin sources resolved to store paths.  Useful for rendering offline pin
  # files (see ekaos/modules/system/pins.nix).
  pins = builtins.mapAttrs (_: v: v.outPath) (import ./pins.nix);

  tests = { };

  # Nix's builtin fetcher
  fetchurl-bootstrap = import ./pkgs/fetchurl/bootstrap.nix {
    inherit (stdenv.buildPlatform) system;
  };

  # The full-source bootstrap: a toolchain grown from the hex0 seed in
  # stage0-posix rather than from a prebuilt tarball. It lives in its own scope
  # so that a stray `callPackage` cannot reach a top-level package and quietly
  # reintroduce the binary seed it exists to avoid.
  #
  # The scope is built in `stdenv/linux/stage0.nix` and surfaced here, rather
  # than constructed a second time: on a system that bootstraps from source this
  # is the very toolchain `stdenv` was grown from, not a rebuild of it.
  minimal-bootstrap = stdenv.stage0.minimal-bootstrap or null;

  minimal-bootstrap-sources =
    callPackage ./stdenv/minimal-bootstrap/stage0-posix/bootstrap-sources.nix
      {
        inherit (stdenv) hostPlatform;
      };

  make-minimal-bootstrap-sources =
    callPackage ./stdenv/minimal-bootstrap/stage0-posix/make-bootstrap-sources.nix
      {
        inherit (stdenv) hostPlatform;
      };

  nixos = null;

  haskell = callPackage ./haskell { inherit config; };
  haskellPackages = haskell.packages.ghc984Binary;

  # qemu_kvm - QEMU with only host CPU support (for vmTools)
  # This is required for vmTools to work correctly with direct kernel boot
  qemu_kvm = lib.lowPrio (qemu.override { hostCpuOnly = true; });

  # ekaosTest - Testing framework for ekaos systems
  ekaosTest = (callPackage ./ekaos/lib/testing { }).runTest;

  # ekaosTests - Test suite for ekaos systems (individual tests can be built)
  ekaosTests = callPackage ./ekaos/tests { };

  # integrationTests - Unified entry point for all integration test frameworks
  # Includes runitTests, ekaosTests, and future test frameworks
  integrationTests = callPackage ./integration-tests { };

  # mkDevShell - Development shell with service management
  # Creates development environments with running services using ekaos modules
  mkDevShell = (callPackage ./dev-shell { }).mkDevShell;

  nix-prefetch-git = callPackage ./pkgs/nix-prefetch-git { git = gitMinimal; };

  freshBootstrapTools =
    if stdenv.hostPlatform.isDarwin then
      import ./stdenv/darwin/make-bootstrap-tools.nix {
        localSystem = stdenv.buildPlatform;
        crossSystem = stdenv.hostPlatform;
      }
    else
      import ./stdenv/linux/make-bootstrap-tools.nix { pkgs = final; };

  gstreamerPkgs = lib.recurseIntoAttrs gstreamer.pkgs;

  # nv-codec-headers version aliases for ffmpeg
  nv-codec-headers-12 = nv-codec-headers.override { majorVersion = "12"; };

  mpi = null; # fftwMpi, boost

  # Darwin packages use the ordinary package scope and shared package directories.
  bootstrapStdenv = stdenv.override (old: {
    extraBuildInputs = map (
      pkg:
      if lib.isDerivation pkg && lib.getName pkg == "apple-sdk" then
        pkg.override { enableBootstrap = true; }
      else
        pkg
    ) (old.extraBuildInputs or [ ]);
  });

  inherit (llvmPackages) clang-unwrapped;
  inherit (file_cmds) xattr;
  xarMinimal = callPackage ./pkgs/xar { e2fsprogs = null; };

  apple-sdk_14 = apple-sdk.override { darwinSdkMajorVersion = "14"; };
  apple-sdk_15 = apple-sdk.override { darwinSdkMajorVersion = "15"; };
  apple-sdk_26 = apple-sdk.override { darwinSdkMajorVersion = "26"; };
  inherit (callPackage ./pkgs/xcode { })
    requireXcode
    xcode_8_1
    xcode_8_2
    xcode_9_1
    xcode_9_2
    xcode_9_3
    xcode_9_4
    xcode_9_4_1
    xcode_10_1
    xcode_10_2
    xcode_10_2_1
    xcode_10_3
    xcode_11
    xcode_11_1
    xcode_11_2
    xcode_11_3
    xcode_11_3_1
    xcode_11_4
    xcode_11_5
    xcode_11_6
    xcode_11_7
    xcode_12
    xcode_12_0_1
    xcode_12_1
    xcode_12_2
    xcode_12_3
    xcode_12_4
    xcode_12_5
    xcode_12_5_1
    xcode_13
    xcode_13_1
    xcode_13_2
    xcode_13_2_1
    xcode_13_3
    xcode_13_3_1
    xcode_13_4
    xcode_13_4_1
    xcode_14
    xcode_14_1
    xcode_15
    xcode_15_0_1
    xcode_15_1
    xcode_15_2
    xcode_15_3
    xcode_15_4
    xcode_16
    xcode_16_1
    xcode_16_2
    xcode_16_3
    xcode_16_4
    xcode_26
    xcode_26_Apple_silicon
    xcode_26_0_1
    xcode_26_0_1_Apple_silicon
    xcode_26_1
    xcode_26_1_Apple_silicon
    xcode_26_1_1
    xcode_26_1_1_Apple_silicon
    xcode_26_2
    xcode_26_2_Apple_silicon
    xcode_26_3
    xcode_26_3_Apple_silicon
    xcode_26_4
    xcode_26_4_Apple_silicon
    xcode_26_4_1
    xcode_26_4_1_Apple_silicon
    xcode_26_5
    xcode_26_5_Apple_silicon
    xcode_26_6
    xcode_26_6_Apple_silicon
    xcode
    ;

  # TODO(corepkgs): support windows
  windows = null;
  libgnurx = null;

  # Non-GNU/Linux OSes are currently "impure" platforms, with their libc
  # outside of the store.  Thus, GCC, GFortran, & co. must always look for files
  # in standard system directories (/usr/include, etc.)
  noSysDirs =
    stdenv.buildPlatform.system != "x86_64-solaris"
    && stdenv.buildPlatform.system != "x86_64-kfreebsd-gnu";

  mkManyVariants = callFromScope ./pkgs/mkManyVariants { };

  # A stdenv capable of building 32-bit binaries.
  # On x86_64-linux, it uses GCC compiled with multilib support; on i686-linux,
  # it's just the plain stdenv.
  stdenv_32bit = lib.lowPrio (if stdenv.hostPlatform.is32bit then stdenv else multiStdenv);

  mkStdenvNoLibs =
    stdenv:
    let
      bintools = stdenv.cc.bintools.override {
        libc = null;
        noLibc = true;
      };
    in
    stdenv.override {
      cc = stdenv.cc.override {
        libc = null;
        noLibc = true;
        extraPackages = [ ];
        inherit bintools;
      };
      allowedRequisites = lib.mapNullable (rs: rs ++ [ bintools ]) (stdenv.allowedRequisites or null);
    };

  stdenvNoLibs =
    if stdenvNoCC.hostPlatform != stdenvNoCC.buildPlatform then
      # We cannot touch binutils or cc themselves, because that will cause
      # infinite recursion. So instead, we just choose a libc based on the
      # current platform. That means we won't respect whatever compiler was
      # passed in with the stdenv stage argument.
      #
      # TODO It would be much better to pass the `stdenvNoCC` and *unwrapped*
      # cc, bintools, compiler-rt equivalent, etc. and create all final stdenvs
      # as part of the stage. Then we would never be tempted to override a later
      # thing to to create an earlier thing (leading to infinite recursion) and
      # we also would still respect the stage arguments choices for these
      # things.
      (
        if stdenvNoCC.hostPlatform.isDarwin || stdenvNoCC.hostPlatform.useLLVM or false then
          overrideCC stdenvNoCC buildPackages.llvmPackages.clangNoCompilerRt
        else
          gccCrossLibcStdenv
      )
    else
      mkStdenvNoLibs stdenv;

  stdenvNoLibc =
    if stdenvNoCC.hostPlatform != stdenvNoCC.buildPlatform then
      (
        if stdenvNoCC.hostPlatform.isDarwin || stdenvNoCC.hostPlatform.useLLVM or false then
          overrideCC stdenvNoCC buildPackages.llvmPackages.clangNoLibc
        else
          gccCrossLibcStdenv
      )
    else
      mkStdenvNoLibs stdenv;

  gccStdenvNoLibs = mkStdenvNoLibs gccStdenv;
  clangStdenvNoLibs = mkStdenvNoLibs clangStdenv;

  glibc = callPackage ./pkgs/glibc (
    if stdenv.hostPlatform != stdenv.buildPlatform then
      {
        stdenv = gccCrossLibcStdenv; # doesn't compile without gcc
        # Separate from pkgs/gcc/common/libgcc.nix — different bootstrap stage
        libgcc = callPackage ./pkgs/glibc/libgcc-for-glibc.nix {
          gcc = gccCrossLibcStdenv.cc;
          glibc = glibc.override { libgcc = null; };
          stdenvNoLibs = gccCrossLibcStdenv;
        };
      }
    else
      {
        stdenv = gccStdenv; # doesn't compile without gcc
      }
  );

  # Only supported on Linux and only on glibc
  glibcLocales =
    if stdenv.hostPlatform.isLinux && stdenv.hostPlatform.isGnu then
      callPackage ./pkgs/glibc/locales.nix {
        stdenv = if (!stdenv.cc.isGNU) then gccStdenv else stdenv;
        withLinuxHeaders = !stdenv.cc.isGNU;
      }
    else
      null;
  glibcLocalesUtf8 =
    if stdenv.hostPlatform.isLinux && stdenv.hostPlatform.isGnu then
      callPackage ./pkgs/glibc/locales.nix {
        stdenv = if (!stdenv.cc.isGNU) then gccStdenv else stdenv;
        withLinuxHeaders = !stdenv.cc.isGNU;
        allLocales = false;
      }
    else
      null;

  glibcInfo = callPackage ./pkgs/glibc/info.nix { };

  glibc_multi = callPackage ./pkgs/glibc/multi.nix {
    # The buildPackages is required for cross-compilation. The pkgsi686Linux set
    # has target and host always set to the same value based on target platform
    # of the current set. We need host to be same as build to correctly get i686
    # variant of glibc.
    glibc32 = pkgsi686Linux.buildPackages.glibc;
  };

  libc =
    let
      inherit (stdenv.hostPlatform) libc;
      # libc is hackily often used from the previous stage. This `or`
      # hack fixes the hack, *sigh*.
    in
    if libc == null then
      null
    else if libc == "glibc" then
      glibc
    else if libc == "bionic" then
      bionic
    else if libc == "uclibc" then
      uclibc
    else if libc == "avrlibc" then
      avrlibc
    else if libc == "newlib" && stdenv.hostPlatform.isMsp430 then
      msp430Newlib
    else if libc == "newlib" && stdenv.hostPlatform.isVc4 then
      vc4-newlib
    else if libc == "newlib" && stdenv.hostPlatform.isOr1k then
      or1k-newlib
    else if libc == "newlib" then
      newlib
    else if libc == "newlib-nano" then
      newlib-nano
    else if libc == "musl" then
      musl
    else if libc == "msvcrt" then
      windows.mingw_w64
    else if libc == "ucrt" then
      windows.mingw_w64
    else if libc == "libSystem" then
      if stdenv.hostPlatform.useiOSPrebuilt then iosSdkPkgs.libraries else libSystem
    else if libc == "fblibc" then
      freebsd.libc
    else if libc == "oblibc" then
      openbsd.libc
    else if libc == "nblibc" then
      netbsd.libc
    else if libc == "wasilibc" then
      wasilibc
    else if libc == "relibc" then
      relibc
    else if name == "llvm" then
      llvmPackages_20.libc
    else
      throw "Unknown libc ${libc}";

  lit = with python3Packages; toPythonApplication lit;

  binutils_nogold = lib.lowPrio (wrapBintoolsWith {
    bintools = binutils.unwrapped.real.override {
      enableGold = false;
    };
  });

  libbfd = callPackage ./pkgs-many/binutils/libbfd.nix { };

  libopcodes = callPackage ./pkgs-many/binutils/libopcodes.nix { };

  libbfd_2_38 = callPackage ./pkgs-many/binutils/2.38/libbfd.nix {
    autoreconfHook = buildPackages.autoconf.v2_69.autoreconfHook;
  };

  libopcodes_2_38 = callPackage ./pkgs-many/binutils/2.38/libopcodes.nix {
    autoreconfHook = buildPackages.autoconf.v2_69.autoreconfHook;
  };

  # Here we select the default bintools implementations to be used.  Note when
  # cross compiling these are used not for this stage but the *next* stage.
  # That is why we choose using this stage's target platform / next stage's
  # host platform.
  #
  # Because this is the *next* stages choice, it's a bit non-modular to put
  # here. In theory, bootstrapping is supposed to not be a chain but at tree,
  # where each stage supports many "successor" stages, like multiple possible
  # futures. We don't have a better alternative, but with this downside in
  # mind, please be judicious when using this attribute. E.g. for building
  # things in *this* stage you should use probably `stdenv.cc.bintools` (from a
  # default or alternate `stdenv`), at build time, and try not to "force" a
  # specific bintools at runtime at all.
  #
  # In other words, try to only use this in wrappers, and only use those
  # wrappers from the next stage.
  bintools-unwrapped =
    let
      inherit (stdenv.targetPlatform) linker;
    in
    if linker == "lld" then
      llvmPackages.bintools-unwrapped
    else if linker == "cctools" then
      binutils.unwrapped
    else if linker == "bfd" then
      binutils.unwrapped
    else if linker == "gold" then
      binutils.unwrapped.override { enableGoldDefault = true; }
    else
      null;
  bintoolsNoLibc = wrapBintoolsWith {
    bintools = bintools-unwrapped;
    libc = targetPackages.preLibcHeaders or preLibcHeaders;
  };
  bintools = wrapBintoolsWith {
    bintools = bintools-unwrapped;
  };

  xorg =
    let
      # Backward-compatibility alias set: maps legacy xorg.* attr names to top-level packages.
      aliases = import ./pkgs/xorg { inherit lib; };
      aliasSet = aliases __splicedPackages;
    in
    lib.recurseIntoAttrs aliasSet;

  zlib-ng-compat = zlib-ng.override { withZlibCompat = true; };

  makeDBusConf = callPackage ./pkgs/dbus/make-dbus-conf.nix { };

  fetchpatch =
    callPackage ./pkgs/fetchpatch {
      # 0.3.4 would change hashes: https://github.com/NixOS/nixpkgs/issues/25154
      patchutils = __splicedPackages.patchutils_0_3_3;
    }
    // {
      version = 1;
    };
  fetchpatch2 =
    callPackage ./pkgs/fetchpatch {
      patchutils = __splicedPackages.patchutils_0_4_2;
    }
    // {
      version = 2;
    };
  # TODO: proper freebsd port
  freebsd = { };

  inherit (callPackages ./pkgs/patchRcPathHooks { })
    patchRcPathBash
    patchRcPathCsh
    patchRcPathFish
    patchRcPathPosix
    ;

  json-schema-for-humans = with python3Packages; toPythonApplication json-schema-for-humans;

  libGLU = mesa_glu;
  libjack2 = jack2.override { prefix = "lib"; };
  libdbusmenu-gtk3 = libdbusmenu.gtk3;
  libglut = freeglut;
  libva-minimal = callPackage ./pkgs/libva { minimal = true; };
  mesa_i686 = pkgsi686Linux.mesa;
  libgbm = callPackage ./pkgs/mesa/gbm.nix { };
  mesa-gl-headers = callPackage ./pkgs/mesa/headers.nix { };

  # variant of qemu building user space emulator only - intended to be used from pkgsStatic
  qemu-user = qemu.override {
    userOnly = true;
  };

  wrapQemuBinfmtP = callPackage ./pkgs/qemu/binfmt-p-wrapper.nix { };

  libxcrypt = callPackage ./pkgs/libxcrypt {
    fetchurl = fetchurl-bootstrap;
    perl = buildPackages.perl.override {
      enableCrypt = false;
      fetchurl = fetchurl-bootstrap;
    };
  };

  libjpeg = libjpeg_turbo;

  # Less secure variant of lowdown for use inside Nix builds.
  lowdown-unsandboxed = lowdown.override {
    enableDarwinSandbox = false;
  };

  # These are used when building compiler-rt / libgcc, prior to building libc.
  preLibcHeaders =
    let
      inherit (stdenv.hostPlatform) libc;
    in
    if stdenv.hostPlatform.isMinGW then
      windows.mingw_w64_headers or fallback
    else if libc == "nblibc" then
      netbsd.headers
    else if libc == "cygwin" then
      cygwin.newlib-cygwin-headers
    else
      null;

  po4a = perlPackages.Po4a;

  inherit (callPackages ./pkgs/fetchYarnDeps { })
    fetchYarnDeps
    fixup-yarn-lock
    prefetch-yarn-deps
    yarnConfigHook
    yarnBuildHook
    yarnInstallHook
    ;

  procps = if stdenv.hostPlatform.isLinux then procps-ng else unixtools.procps;

  default-gcc-version = 14;
  gcc = pkgs.${"gcc${toString default-gcc-version}"};
  gccFun = callPackage ./pkgs/gcc;
  gcc-unwrapped = gcc.cc;
  libgcc = stdenv.cc.cc.libgcc or null;

  # This is for e.g. LLVM libraries on linux.
  gccForLibs =
    if
      stdenv.targetPlatform == stdenv.hostPlatform && targetPackages.stdenv.cc.isGNU
    # Can only do this is in the native case, otherwise we might get infinite
    # recursion if `targetPackages.stdenv.cc.cc` itself uses `gccForLibs`.
    then
      targetPackages.stdenv.cc.cc
    else
      gcc.cc;

  inherit
    (rec {
      # NOTE: keep this with the "NG" label until we're ready to drop the monolithic GCC
      gccNGPackagesSet = lib.recurseIntoAttrs (callPackages ./pkgs/gcc/ng { });
      gccNGPackages_15 = gccNGPackagesSet."15";
      mkGCCNGPackages = gccNGPackagesSet.mkPackage;
    })
    gccNGPackages_15
    mkGCCNGPackages
    ;

  wrapNonDeterministicGcc =
    stdenv: ccWrapper:
    if ccWrapper.isGNU then
      ccWrapper.overrideAttrs (old: {
        env = old.env // {
          cc = old.env.cc.override {
            reproducibleBuild = false;
            profiledCompiler = with stdenv.hostPlatform; (!isDarwin && isx86);
          };
        };
      })
    else
      ccWrapper;

  gfortran = wrapCC (
    gcc.cc.override {
      name = "gfortran";
      langFortran = true;
      langCC = false;
      langC = false;
      profiledCompiler = false;
    }
  );

  gobject-introspection-unwrapped = callPackage ./pkgs/gobject-introspection/unwrapped.nix { };

  buildGoModule = go.buildModule;

  buildZigPackage = zig.buildZigPackage;

  rPackages = callPackage ./r { inherit config; };

  buildNpmPackage = nodejs.buildNpmPackage;

  gnuStdenv =
    if stdenv.cc.isGNU then
      stdenv
    else
      gccStdenv.override {
        cc = gccStdenv.cc.override {
          bintools = buildPackages.binutils;
        };
      };

  gccStdenv =
    if stdenv.cc.isGNU then
      stdenv
    else
      stdenv.override {
        cc = buildPackages.gcc;
        allowedRequisites = null;
        # Remove libcxx/libcxxabi, and add clang for AS if on darwin (it uses
        # clang's internal assembler).
        extraBuildInputs = lib.optional stdenv.hostPlatform.isDarwin clang.cc;
      };

  gcc13Stdenv = overrideCC gccStdenv buildPackages.gcc13;
  gcc14Stdenv = overrideCC gccStdenv buildPackages.gcc14;
  gcc15Stdenv = overrideCC gccStdenv buildPackages.gcc15;

  # This is not intended for use in nixpkgs but for providing a faster-running
  # compiler to nixpkgs users by building gcc with reproducibility-breaking
  # profile-guided optimizations
  fastStdenv = overrideCC gccStdenv (wrapNonDeterministicGcc gccStdenv buildPackages.gcc_latest);

  wrapCCMulti =
    cc:
    let
      # Binutils with glibc multi
      bintools = cc.bintools.override {
        libc = glibc_multi;
      };
    in
    lib.lowPrio (wrapCCWith {
      cc = cc.cc.override {
        stdenv = overrideCC stdenv (wrapCCWith {
          cc = cc.cc;
          inherit bintools;
          libc = glibc_multi;
        });
        profiledCompiler = false;
        enableMultilib = true;
      };
      libc = glibc_multi;
      inherit bintools;
      extraBuildCommands = ''
        echo "dontMoveLib64=1" >> $out/nix-support/setup-hook
      '';
    });

  # TODO(corepkgs): port llvm/multi.nix for clang multilib support
  wrapClangMulti =
    clang: throw "clang_multi is not yet available in core-pkgs (needs llvm/multi.nix ported)";

  gcc_multi = wrapCCMulti gcc;
  clang_multi = wrapClangMulti clang;

  gccMultiStdenv = overrideCC stdenv buildPackages.gcc_multi;
  clangMultiStdenv = overrideCC stdenv buildPackages.clang_multi;
  multiStdenv = if stdenv.cc.isClang then clangMultiStdenv else gccMultiStdenv;

  gcc_debug = lib.lowPrio (
    wrapCC (
      gcc.cc.overrideAttrs {
        dontStrip = true;
      }
    )
  );

  gccCrossLibcStdenv = overrideCC stdenvNoCC buildPackages.gccWithoutTargetLibc;

  # The GCC used to build libc for the target platform. Normal gccs will be
  # built with, and use, that cross-compiled libc.
  gccWithoutTargetLibc =
    let
      libc1 = binutils.noLibc.libc;
    in
    (wrapCCWith {
      cc = gccFun {
        # copy-pasted
        inherit noSysDirs;
        majorMinorVersion = toString default-gcc-version;

        reproducibleBuild = true;
        profiledCompiler = false;

        isl = if !stdenv.hostPlatform.isDarwin then isl else null;

        withoutTargetLibc = true;
        langCC = stdenv.targetPlatform.isCygwin; # can't compile libcygwin1.a without C++
        libcCross = libc1;
        targetPackages.stdenv.cc.bintools = binutils.noLibc;
        enableShared =
          stdenv.targetPlatform.hasSharedLibraries

          # temporarily disabled due to breakage;
          # see https://github.com/NixOS/nixpkgs/pull/243249
          && !stdenv.targetPlatform.isWindows
          && !stdenv.targetPlatform.isCygwin
          && !(stdenv.targetPlatform.useLLVM or false);
      };
      bintools = binutils.noLibc;
      libc = libc1;
      extraPackages = [ ];
    }).overrideAttrs
      (prevAttrs: {
        meta = prevAttrs.meta // {
          badPlatforms =
            (prevAttrs.meta.badPlatforms or [ ])
            ++ lib.optionals (stdenv.targetPlatform == stdenv.hostPlatform) [ stdenv.hostPlatform.system ];
        };
      });

  # gcc-releases is auto-imported from pkgs-many/gcc-releases/ via mkManyVariants
  gcc13 = gcc-releases.v13;
  gcc14 = gcc-releases.v14;
  gcc15 = gcc-releases.v15;
  gcc_latest = gcc15;

  libgccjit = gcc.cc.override {
    name = "libgccjit";
    langFortran = false;
    langCC = false;
    langC = false;
    profiledCompiler = false;
    langJit = true;
    enableLTO = false;
  };

  # Utility to extract just the source from a derivation
  srcOnly =
    args:
    (callPackage (
      {
        runCommand,
        lib,
        stdenvNoCC,
      }:
      drv:
      runCommand "${drv.name}-src"
        {
          outputs = [ "out" ];
          preferLocalBuild = true;
        }
        ''
          mkdir -p $out
          ${lib.concatMapStringsSep "\n" (output: ''
            if [ -d "${drv.${output}}" ]; then
              cp -r "${drv.${output}}"/* $out/
            fi
          '') (drv.outputs or [ "out" ])}
        ''
    ) { } args);

  wrapCCWith =
    {
      cc,
      # This should be the only bintools runtime dep with this sort of logic. The
      # Others should instead delegate to the next stage's choice with
      # `targetPackages.stdenv.cc.bintools`. This one is different just to
      # provide the default choice, avoiding infinite recursion.
      # See the bintools attribute for the logic and reasoning. We need to provide
      # a default here, since eval will hit this function when bootstrapping
      # stdenv where the bintools attribute doesn't exist, but will never actually
      # be evaluated -- callPackage ends up being too eager.
      bintools ? pkgs.bintools,
      libc ? bintools.libc,
      # libc++ from the default LLVM version is bound at the top level, but we
      # want the C++ library to be explicitly chosen by the caller, and null by
      # default.
      libcxx ? null,
      extraPackages ? [ ],
      nixSupport ? { },
      ...
    }@extraArgs:
    callPackage ./stdenv/cc-wrapper (
      let
        self = {
          nativeTools = stdenv.targetPlatform == stdenv.hostPlatform && stdenv.cc.nativeTools or false;
          nativeLibc = stdenv.targetPlatform == stdenv.hostPlatform && stdenv.cc.nativeLibc or false;
          nativePrefix = stdenv.cc.nativePrefix or "";
          noLibc = !self.nativeLibc && (self.libc == null);

          isGNU = cc.isGNU or false;
          isClang = cc.isClang or false;
          isArocc = cc.isArocc or false;
          isZig = cc.isZig or false;

          inherit
            lib
            cc
            bintools
            libc
            libcxx
            extraPackages
            nixSupport
            ;
        }
        // extraArgs;
      in
      self
    );
  wrapCC =
    cc:
    wrapCCWith {
      inherit cc;
    };
  wrapBintoolsWith =
    {
      bintools,
      libc ? targetPackages.libc or pkgs.libc,
      ...
    }@extraArgs:
    callPackage ./stdenv/bintools-wrapper (
      let
        self = {
          nativeTools = stdenv.targetPlatform == stdenv.hostPlatform && stdenv.cc.nativeTools or false;
          nativeLibc = stdenv.targetPlatform == stdenv.hostPlatform && stdenv.cc.nativeLibc or false;
          nativePrefix = stdenv.cc.nativePrefix or "";

          noLibc = (self.libc == null);

          inherit bintools libc;
        }
        // extraArgs;
      in
      self
    );

  # Helper tools for dockerTools

  runUnitTests = pkg: pkg.overrideAttrs { doCheck = true; };
  runtimeShell = "${runtimeShellPackage}${runtimeShellPackage.shellPath}";
  runtimeShellPackage = bashNonInteractive;
  bash = callPackage ./pkgs/bash/5.nix { };
  bashNonInteractive = lib.lowPrio (
    callPackage ./pkgs/bash/5.nix {
      interactive = false;
    }
  );
  # WARNING: this attribute is used by nix-shell so it shouldn't be removed/renamed
  bashInteractive = bash;
  bashFHS = callPackage ./pkgs/bash/5.nix {
    forFHSEnv = true;
  };

  python = python3;
  python2 = python27;
  python3 = python313;

  python2Packages = lib.dontRecurseIntoAttrs python27.pkgs;
  python3Packages = lib.dontRecurseIntoAttrs python313.pkgs;

  pypy = pypy2;
  pypy2 = pypy27;
  pypy3 = pypy311;

  # Python interpreter that is build with all modules, including tkinter.
  # These are for compatibility and should not be used inside Nixpkgs.
  python2Full = python2.override {
    self = python2Full;
    pythonAttr = "python2Full";
    x11Support = true;
  };
  python27Full = python27.override {
    self = python27Full;
    pythonAttr = "python27Full";
    x11Support = true;
  };

  # https://py-free-threading.github.io
  python313FreeThreading = python313.override {
    self = python313FreeThreading;
    pythonAttr = "python313FreeThreading";
    enableGIL = false;
  };
  python314FreeThreading = python314.override {
    self = python314FreeThreading;
    pythonAttr = "python314FreeThreading";
    enableGIL = false;
  };
  python315FreeThreading = python315.override {
    self = python315FreeThreading;
    pythonAttr = "python315FreeThreading";
    enableGIL = false;
  };

  pythonInterpreters = callPackage ./python { inherit config; };
  inherit (pythonInterpreters)
    python27
    python310
    python311
    python312
    python313
    python314
    python315
    python3Minimal
    pypy27
    pypy310
    pypy311
    ;

  # Python package sets.
  python312Packages = lib.recurseIntoAttrs python312.pkgs;
  python313Packages = lib.recurseIntoAttrs python313.pkgs;

  pythonManylinuxPackages = callPackage ./python/manylinux { };

  pythonCondaPackages = callPackage ./python/conda { };

  # Should eventually be moved inside Python interpreters.
  python-setup-hook = buildPackages.callPackage ./python/setup-hook.nix { };

  pythonDocs = lib.recurseIntoAttrs (callPackage ./python/cpython/docs { });

  # Provided by libc on Operating Systems that use the Extensible Linker Format.
  elf-header = if stdenv.hostPlatform.isElf then null else elf-header-real;

  inherit
    (callPackages ./pkgs/linux-support/pkgs/kernel-headers { inherit (pkgsBuildBuild) elf-header; })
    linuxHeaders
    makeLinuxHeaders
    ;

  # while building documentation meson may want to run binaries for host
  # which needs an emulator
  # example of an error which this fixes
  # [Errno 8] Exec format error: './gdk3-scan'
  mesonEmulatorHook =
    makeSetupHook
      {
        name = "mesonEmulatorHook";
        substitutions = {
          crossFile = writeText "cross-file.conf" ''
            [binaries]
            exe_wrapper = '${lib.escape [ "'" "\\" ] (stdenv.targetPlatform.emulator pkgs)}'
          '';
        };
      }
      # The throw is moved into the `makeSetupHook` derivation, so that its
      # outer level, but not its outPath can still be evaluated if the condition
      # doesn't hold. This ensures that splicing still can work correctly.
      (
        if (!stdenv.hostPlatform.canExecute stdenv.targetPlatform) then
          ./pkgs-many/meson/emulator-hook.sh
        else
          throw "mesonEmulatorHook may only be added to nativeBuildInputs when the target binaries can't be executed; however you are attempting to use it in a situation where ${stdenv.hostPlatform.config} can execute ${stdenv.targetPlatform.config}. Consider only adding mesonEmulatorHook according to a conditional based canExecute in your package expression."
      );

  # The coreutils above is built with dependencies from
  # bootstrapping. We cannot override it here, because that pulls in
  # openssl from the previous stage as well.
  coreutils-full = callPackage ./pkgs/coreutils { minimal = false; };
  coreutils-prefixed = coreutils.override {
    withPrefix = true;
    singleBinary = false;
  };

  ipu6ep-camera-hal = ipu6-camera-hal.override {
    ipuVersion = "ipu6ep";
  };

  ipu6epmtl-camera-hal = ipu6-camera-hal.override {
    ipuVersion = "ipu6epmtl";
  };

  ipu75xa-camera-hal = ipu7-camera-hal.override {
    ipuVersion = "ipu75xa";
  };

  openssl_legacy = openssl.override {
    conf = ./pkgs-many/openssl/3.0/legacy.cnf;
  };

  arrayUtilities =
    let
      arrayUtilitiesPackages = makeScopeWithSplicing' {
        otherSplices = generateSplicesForMkScope "arrayUtilities";
        f =
          finalArrayUtilities:
          {
            callPackages = lib.callPackagesWith (pkgs // finalArrayUtilities);
          }
          // lib.packagesFromDirectoryRecursive {
            inherit (finalArrayUtilities) callPackage;
            directory = ./pkgs/arrayUtilities;
          };
      };
    in
    lib.recurseIntoAttrs arrayUtilitiesPackages;
  makeWrapper = makeShellWrapper;

  # Driver-wrapper: lets nix-built executables that need hardware
  # acceleration run on non-NixOS hosts by exposing host-provided
  # GL/Vulkan/CUDA/etc. libraries at runtime.
  #
  # Use as:
  #   nativeBuildInputs = [ wrapDriverProgramHook ];
  #   # autoWrapDriverPrograms is registered into postFixup automatically;
  #   # alternatively call wrapDriverProgram $out/bin/foo manually.
  #
  # Or, externally, transform an existing derivation:
  #   wrapDriverProgram { drv = somepkg; }
  wrapDriverProgramHook = callPackage ./build-support/wrap-driver-program { };
  wrapDriverProgram = wrapDriverProgramHook.wrapDriverProgram;
  autoWrapDriverPrograms = wrapDriverProgramHook;

  readline70 = callPackage ./pkgs/readline/7.0.nix { };
  readline = callPackage ./pkgs/readline/8.3.nix { };

  util-linuxMinimal = util-linux.override {
    cryptsetupSupport = false;
    nlsSupport = false;
    ncursesSupport = false;
    pamSupport = false;
    shadowSupport = false;
    systemdSupport = false;
    translateManpages = false;
    withLastlog = false;
  };

  perlPackages = perl.pkgs;

  # BEAM (Erlang/Elixir) package scope — builders, fetchers, hooks, and tools
  # parameterized by the default erlang version. For other versions:
  # erlang.v28.beamPackages, erlang.v29.beamPackages, etc.
  beamPackages = lib.dontRecurseIntoAttrs erlang.beamPackages;

  # On non-GNU systems we need GNU Gettext for libintl.
  libintl = if stdenv.hostPlatform.libc != "glibc" then gettext else null;

  # libpng is auto-imported from pkgs-many/libpng/ via mkManyVariants
  # Variants: libpng.v1_2, libpng.v1_6 (default)
  libpng12 = prev.libpng.v1_2;

  libpulseaudio = prev.pulseaudio.override {
    libOnly = true;
  };

  genericUpdater = callPackage ./pkgs/common-updater-scripts/generic-updater.nix { };
  _experimental-update-script-combinators =
    callPackage ./pkgs/common-updater-scripts/combinators.nix
      { };
  directoryListingUpdater =
    callPackage ./pkgs/common-updater-scripts/directory-listing-updater.nix
      { };
  gitUpdater = callPackage ./pkgs/common-updater-scripts/git-updater.nix { };
  httpTwoLevelsUpdater = callPackage ./pkgs/common-updater-scripts/http-two-levels-updater.nix { };
  unstableGitUpdater = callPackage ./pkgs/common-updater-scripts/unstable-updater.nix { };

  libuuid = if stdenv.hostPlatform.isLinux then util-linuxMinimal else null;

  ncurses =
    if stdenv.hostPlatform.useiOSPrebuilt then
      null
    else if stdenv.hostPlatform.isDarwin then
      prev.ncurses.override {
        # ncurses is included in the SDK. Avoid an infinite recursion by using a bootstrap stdenv.
        stdenv = bootstrapStdenv;
      }
    else
      prev.ncurses;

  pkgconf = callPackage ./stdenv/pkg-config-wrapper {
    pkg-config = pkgconf-unwrapped;
  };
  pkgconf-unwrapped = callPackage ./pkgs/pkgconf { };
  pkg-config = callPackage ./stdenv/pkg-config-wrapper {
    pkg-config = pkg-config-unwrapped;
  };
  pkg-configUpstream = lib.lowPrio (
    pkg-config.override (old: {
      pkg-config = old.pkg-config.override {
        vanilla = true;
      };
    })
  );

  sqlite = lib.lowPrio (callPackage ./pkgs/sqlite { });
  sqlar = callPackage ./pkgs/sqlite/sqlar.nix { };
  sqlite-interactive = (sqlite.override { interactive = true; }).bin;

  gawk-with-extensions = callPackage ./pkgs/gawk/gawk-with-extensions.nix {
    extensions = gawkextlib.full;
  };
  gawkextlib = callPackage ./pkgs/gawk/gawkextlib.nix { };
  gawkInteractive = gawk.override { interactive = true; };

  pam =
    if stdenv.hostPlatform.isLinux then
      linux-pam
    else if stdenv.hostPlatform.isFreeBSD then
      freebsd.libpam
    else
      openpam;

  systemd = callPackage ./pkgs/linux-support/pkgs/systemd {
    # break some cyclic dependencies
    util-linux = util-linuxMinimal;
    # provide a super minimal gnupg used for systemd-machined
    gnupg = gnupg.override {
      enableMinimal = true;
      guiSupport = false;
    };
  };
  systemdMinimal = systemd.override {
    pname = "systemd-minimal";
    withAcl = false;
    withAnalyze = false;
    withApparmor = false;
    withAudit = false;
    withCompression = false;
    withCoredump = false;
    withCryptsetup = false;
    withRepart = false;
    withDocumentation = false;
    withEfi = false;
    withFido2 = false;
    withGcrypt = false;
    withHostnamed = false;
    withHomed = false;
    withHwdb = false;
    withImportd = false;
    withLibBPF = false;
    withLibidn2 = false;
    withLocaled = false;
    withLogind = false;
    withMachined = false;
    withNetworkd = false;
    withNss = false;
    withOomd = false;
    withOpenSSL = false;
    withPCRE2 = false;
    withPam = false;
    withPolkit = false;
    withPortabled = false;
    withRemote = false;
    withResolved = false;
    withShellCompletions = false;
    withSysupdate = false;
    withSysusers = false;
    withTimedated = false;
    withTimesyncd = false;
    withTpm2Tss = false;
    withUserDb = false;
    withUkify = false;
    withBootloader = false;
    withPasswordQuality = false;
    withVmspawn = false;
    withQrencode = false;
    withLibarchive = false;
    withVConsole = false;
    # withKmod = false; # breaks udevCheckHook of bcache-tools
    withFirstboot = false;
    withKexectools = false;
    withLibseccomp = false;
    withNspawn = false;
  };
  systemdLibs = systemdMinimal.override {
    pname = "systemd-minimal-libs";
    buildLibsOnly = true;
  };
  # We do not want to include ukify in the normal systemd attribute as it
  # relies on Python at runtime.
  systemdUkify = systemd.override {
    pname = "systemd-ukify";
    withUkify = true;
  };
  # docbook-xsl is auto-imported from pkgs-many/docbook-xsl/ via mkManyVariants
  # Variants: docbook-xsl.nons (default), docbook-xsl.ns
  docbook-xsl-nons = docbook-xsl.nons;
  docbook-xsl-ns = docbook-xsl.ns;

  # libxml2 is auto-imported from pkgs-many/libxml2/ via mkManyVariants
  # Variants: libxml2.v2_13, libxml2.v2_15 (default)
  libxml2_13 = prev.libxml2.v2_13;

  c-aresMinimal = callPackage ./pkgs/c-ares { withCMake = false; };

  libkrb5 = krb5;

  ngtcp2-gnutls = callPackage ./pkgs/ngtcp2/gnutls.nix { };

  patchutils_0_3_3 = callPackage ./pkgs/patchutils/0.3.3.nix { };
  patchutils_0_4_2 = callPackage ./pkgs/patchutils/0.4.2.nix { };

  git = callPackage ./pkgs/git {
    perlLibs = [
      perlPackages.LWP
      perlPackages.URI
      perlPackages.TermReadKey
    ];
    smtpPerlLibs = [
      perlPackages.libnet
      perlPackages.NetSMTPSSL
      perlPackages.IOSocketSSL
      perlPackages.NetSSLeay
      perlPackages.AuthenSASL
      perlPackages.DigestHMAC
    ];
  };

  # The full-featured Git.
  gitFull = git.override {
    svnSupport = false;
    guiSupport = true;
    sendEmailSupport = !stdenv.isCross;
    withSsh = true;
    withLibsecret = !stdenv.hostPlatform.isDarwin;
  };

  git-doc = lib.addMetaAttrs {
    description = "Additional documentation for Git";
    longDescription = ''
      This package contains additional documentation (HTML and text files) that
      is referenced in the man pages of Git.
    '';
  } gitFull.doc;

  gitMinimal = git.override {
    withManual = false;
    osxkeychainSupport = false;
    pythonSupport = false;
    perlSupport = false;
    withpcre2 = false;
  };

  deterministic-host-uname = deterministic-uname.override {
    forPlatform = stdenv.targetPlatform; # offset by 1 so it works in nativeBuildInputs
  };

  gtk3 = callPackage ./pkgs/gtk/3.x.nix {
    trackerSupport = false;
    cupsSupport = false;
  };
  gtk4 = callPackage ./pkgs/gtk/4.x.nix { };

  buildcatrust = with python3.pkgs; toPythonApplication buildcatrust;

  docbook_sgml_dtd_31 = callPackage ./pkgs/docbook-sgml-dtd/3.1.nix { };
  docbook_sgml_dtd_41 = callPackage ./pkgs/docbook-sgml-dtd/4.1.nix { };

  docutils = with python3Packages; toPythonApplication docutils;

  opensshPackages = lib.dontRecurseIntoAttrs (callPackage ./pkgs/openssh { });
  openssh = opensshPackages.openssh.override {
    etcDir = "/etc/ssh";
  };
  opensshTest = openssh.tests.openssh;
  opensshWithKerberos = openssh.override {
    withKerberos = true;
  };
  openssh_hpn = opensshPackages.openssh_hpn.override {
    etcDir = "/etc/ssh";
  };
  openssh_hpnWithKerberos = openssh_hpn.override {
    withKerberos = true;
  };
  openssh_gssapi = opensshPackages.openssh_gssapi.override {
    etcDir = "/etc/ssh";
    withKerberos = true;
  };

  unixtools = lib.recurseIntoAttrs (callPackages ./unixtools.nix { });
  inherit (unixtools)
    hexdump
    ps
    logger
    eject
    umount
    mount
    wall
    hostname
    more
    sysctl
    getconf
    getent
    killall
    xxd
    watch
    ;

  sphinx = with python3.pkgs; toPythonApplication sphinx;

  # nixDependencies scope (moved to pkgs-many/nix/, top-level attr kept for splicing)
  nixDependencies = lib.recurseIntoAttrs (callPackage ./pkgs-many/nix/dependencies-scope.nix { });

  # nix is auto-imported from pkgs-many/nix/ via mkManyVariants
  # nix defaults to v2_34 (stable). Variants: nix.v2_28, ..., nix.v2_35, nix.git
  # Access individual components via nixVersions.nixComponents_2_34.nix-store, etc.

  # Backwards-compatible nixVersions scope. The nix_2_* aliases come from the
  # mkManyVariants nix package; the nixComponents_* scopes are pulled from each
  # variant's passthru.pkgs (the spliced component scope built in generic.nix).
  nixVersions =
    let
      addFallbackPathsCheck =
        pkg:
        pkg
        // {
          tests = pkg.tests // {
            nix-fallback-paths =
              runCommand "test-nix-fallback-paths-version-equals-nix-stable"
                {
                  paths = lib.concatStringsSep "\n" (
                    builtins.attrValues (import ./nixos/modules/installer/tools/nix-fallback-paths.nix)
                  );
                }
                ''
                  if [[ "" != $(grep -vE 'nix-([^-]*-)*${
                    lib.strings.replaceStrings [ "." ] [ "\\." ] pkg.version
                  }$' <<< "$paths") ]]; then
                    echo "nix-fallback-paths not up to date with nixVersions.stable (nix-${pkg.version})"
                    echo "The following paths are not up to date:"
                    grep -v 'nix-${pkg.version}$' <<< "$paths"
                    echo
                    echo "Fix it by running:"
                    echo
                    echo "curl https://releases.nixos.org/nix/nix-${pkg.version}/fallback-paths.nix >nixos/modules/installer/tools/nix-fallback-paths.nix"
                    echo
                    exit 1
                  else
                    echo "nix-fallback-paths versions up to date"
                    touch $out
                  fi
                '';
          };
        };

      # Collect nixComponents scopes from each modular variant's passthru.pkgs.
      # v2_28 is monolithic (no component scope), so it's excluded.
      modularVariants = lib.filterAttrs (name: _: lib.hasPrefix "v2_" name && name != "v2_28") (
        import ./pkgs-many/nix/variants.nix
      );

      nixComponentsFromVariants =
        lib.mapAttrs' (
          name: _:
          let
            componentsName = "nixComponents_${lib.removePrefix "v" name}";
          in
          lib.nameValuePair componentsName nix.${name}.pkgs
        ) modularVariants
        // {
          nixComponents_git = nix.git.pkgs;
        };
    in
    lib.recurseIntoAttrs (
      {
        nix_2_28 = nix.v2_28;
        nix_2_29 = nix.v2_29;
        nix_2_30 = nix.v2_30;
        nix_2_31 = nix.v2_31;
        nix_2_32 = nix.v2_32;
        nix_2_33 = nix.v2_33;
        nix_2_34 = nix.v2_34;
        nix_2_35 = nix.v2_35;
        inherit (nix) git;
        latest = nix.v2_35;
        stable = addFallbackPathsCheck nix;
      }
      // nixComponentsFromVariants
    );

  ensureNewerSourcesHook =
    { year }:
    makeSetupHook
      {
        name = "ensure-newer-sources-hook";
      }
      (
        writeScript "ensure-newer-sources-hook.sh" ''
          postUnpackHooks+=(_ensureNewerSources)
          _ensureNewerSources() {
            local r=$sourceRoot
            # Avoid passing option-looking directory to find. The example is diffoscope-269:
            #   https://salsa.debian.org/reproducible-builds/diffoscope/-/issues/378
            [[ $r == -* ]] && r="./$r"
            '${findutils}/bin/find' "$r" \
              '!' -newermt '${year}-01-01' -exec touch -h -d '${year}-01-02' '{}' '+'
          }
        ''
      );
  # Zip file format only allows times after year 1980, which makes e.g. Python
  # wheel building fail with:
  # ValueError: ZIP does not support timestamps before 1980
  ensureNewerSourcesForZipFilesHook = ensureNewerSourcesHook { year = "1980"; };

  clang = llvmPackages.clang;
  clangStdenv = if stdenv.cc.isClang then stdenv else lib.lowPrio llvmPackages.stdenv;
  libcxxStdenv =
    if stdenv.hostPlatform.isDarwin then stdenv else lib.lowPrio llvmPackages.libcxxStdenv;

  # TODO: fix this properly
  # LLVM is auto-imported from pkgs-many/llvm via mkManyVariants
  # llvm defaults to v21 as the LLVM library
  # llvm.pkgs provides the full package scope (clang, lld, lldb, etc.)
  # Individual versions accessible as: llvm.v18, llvm.v19, etc.
  # Package scopes accessible as: llvm.v18.pkgs, llvm.v19.pkgs, etc.
  # Old names like llvmPackages_18, clang_18, etc. are available via aliases/nixpkgs.nix

  llvmPackages = if stdenv.hostPlatform.isDarwin then llvmPackages_21 else llvm.pkgs;
  llvm =
    if stdenv.hostPlatform.isDarwin then
      lib.makeOverridable (lib.mirrorFunctionArgs prev.llvm.override (
        args:
        let
          scope = if args == { } then llvmPackages else llvmPackages.override args;
        in
        lib.fix (
          llvmPackage:
          scope.llvm.overrideAttrs (old: {
            passthru =
              old.passthru or { }
              // prev.llvm.variants
              // {
                inherit (prev.llvm) extendVariants variantArgs;
                pkgs = scope;
                v21 = llvmPackage;
                variants = prev.llvm.variants // {
                  v21 = llvmPackage;
                };
              };
          })
        )
      )) { }
    else
      prev.llvm;

  # Splicing needs the scopes before LLVM derivations can select their
  # dependencies. Construct them directly for cross toolchains to avoid a cycle.
  # Native scopes retain the variant package interface.
  llvmPackages_18 =
    if
      stdenv.buildPlatform != stdenv.targetPlatform
      && (stdenv.buildPlatform.isDarwin || stdenv.targetPlatform.isDarwin)
    then
      callPackage (import ./pkgs-many/llvm/scope.nix (import ./pkgs-many/llvm/variants.nix).v18) { }
    else
      prev.llvm.v18.pkgs;
  llvmPackages_19 =
    if
      stdenv.buildPlatform != stdenv.targetPlatform
      && (stdenv.buildPlatform.isDarwin || stdenv.targetPlatform.isDarwin)
    then
      callPackage (import ./pkgs-many/llvm/scope.nix (import ./pkgs-many/llvm/variants.nix).v19) { }
    else
      prev.llvm.v19.pkgs;
  llvmPackages_20 =
    if
      stdenv.buildPlatform != stdenv.targetPlatform
      && (stdenv.buildPlatform.isDarwin || stdenv.targetPlatform.isDarwin)
    then
      callPackage (import ./pkgs-many/llvm/scope.nix (import ./pkgs-many/llvm/variants.nix).v20) { }
    else
      prev.llvm.v20.pkgs;
  llvmPackages_21 =
    if
      stdenv.buildPlatform != stdenv.targetPlatform
      && (stdenv.buildPlatform.isDarwin || stdenv.targetPlatform.isDarwin)
    then
      callPackage (import ./pkgs-many/llvm/scope.nix (import ./pkgs-many/llvm/variants.nix).v21) { }
    else
      prev.llvm.v21.pkgs;
  llvmPackages_git =
    if
      stdenv.buildPlatform != stdenv.targetPlatform
      && (stdenv.buildPlatform.isDarwin || stdenv.targetPlatform.isDarwin)
    then
      callPackage (import ./pkgs-many/llvm/scope.nix (import ./pkgs-many/llvm/variants.nix).git) { }
    else
      prev.llvm.git.pkgs;

  # Lua is auto-imported from pkgs-many/lua via mkManyVariants
  # lua defaults to v5_4 (Lua 5.4.7) as the Lua interpreter
  # lua.pkgs provides the full Lua package scope (awesome-wm-widgets, etc.)
  # Individual versions accessible as: lua.v5_1, lua.v5_2, lua.v5_3, lua.v5_4, lua.v5_5
  # LuaJIT variants accessible as: lua.luajit_2_0, lua.luajit_2_1, lua.luajit_openresty
  # Package scopes accessible as: lua.v5_3.pkgs, lua.luajit_2_0.pkgs, etc.

  lld = llvmPackages.lld;
  luaPackages = lua.pkgs;

  poppler-utils = poppler.override {
    suffix = "utils";
    utils = true;
  };

  asciidoc = callPackage ./pkgs/asciidoc {
    inherit (python3.pkgs)
      pygments
      matplotlib
      numpy
      aafigure
      recursive-pth-loader
      ;
  };
  # TODO(corepkgs): requires graphviz, lilypond, imagemagick, etc.
  asciidoc-full = throw "asciidoc-full: standard features require graphviz, lilypond, and other packages not yet in core-pkgs";
  asciidoc-full-with-plugins = throw "asciidoc-full-with-plugins: requires packages not yet in core-pkgs";

  imagemagick6_light = imagemagick6.override {
    bzip2Support = false;
    zlibSupport = false;
    libX11Support = false;
    libXtSupport = false;
    fontconfigSupport = false;
    freetypeSupport = false;
    ghostscriptSupport = false;
    libjpegSupport = false;
    djvulibreSupport = false;
    lcms2Support = false;
    openexrSupport = false;
    libpngSupport = false;
    liblqr1Support = false;
    librsvgSupport = false;
    libtiffSupport = false;
    libxml2Support = false;
    openjpegSupport = false;
    libwebpSupport = false;
    libheifSupport = false;
    libde265Support = false;
  };
  imagemagick6 = callPackage ./pkgs/imagemagick/6.x.nix { };
  imagemagick6Big = imagemagick6.override {
    ghostscriptSupport = true;
  };
  imagemagick_light = imagemagick.override {
    bzip2Support = false;
    zlibSupport = false;
    libX11Support = false;
    libXtSupport = false;
    fontconfigSupport = false;
    freetypeSupport = false;
    libraqmSupport = false;
    ghostscriptSupport = false;
    libjpegSupport = false;
    djvulibreSupport = false;
    lcms2Support = false;
    openexrSupport = false;
    libpngSupport = false;
    liblqr1Support = false;
    librsvgSupport = false;
    libtiffSupport = false;
    libxml2Support = false;
    openjpegSupport = false;
    libwebpSupport = false;
    libheifSupport = false;
    libjxlSupport = false;
  };
  imagemagickBig = imagemagick.override {
    ghostscriptSupport = true;
  };

  inherit (texlive.schemes)
    texliveBasic
    texliveBookPub
    texliveConTeXt
    texliveFull
    texliveGUST
    texliveInfraOnly
    texliveMedium
    texliveMinimal
    texliveSmall
    texliveTeTeX
    ;
  texlivePackages = lib.recurseIntoAttrs (lib.mapAttrs (_: v: v.build) texlive.pkgs);

  # Rust is auto-imported from pkgs-many/rust via mkManyVariants
  rustPackages = rust.pkgs;

  inherit (rustPackages)
    cargo
    cargo-auditable
    cargo-auditable-cargo-wrapper
    clippy
    rustc
    rustc-unwrapped
    rustPlatform
    rustfmt
    ;

  makeRustPlatform = callPackage ./pkgs/rust/make-rust-platform.nix { };

  buildRustCrate =
    let
      # Returns a true if the builder's rustc was built with support for the target.
      targetAlreadyIncluded = lib.elem stdenv.hostPlatform.rust.rustcTarget (
        lib.splitString "," (
          lib.removePrefix "--target=" (
            lib.elemAt (lib.filter (
              f: lib.hasPrefix "--target=" f
            ) pkgsBuildBuild.rustc.unwrapped.configureFlags) 0
          )
        )
      );
    in
    callPackage ./pkgs/buildRustCrate (
      { }
      // lib.optionalAttrs (stdenv.hostPlatform.libc == null) {
        stdenv = stdenvNoCC; # Some build targets without libc will fail to evaluate with a normal stdenv.
      }
      // lib.optionalAttrs targetAlreadyIncluded { inherit (pkgsBuildBuild) rustc cargo; } # Optimization.
    );

  inherit (callPackages ./pkgs/cargo-pgrx { })
    cargo-pgrx_0_12_0_alpha_1
    cargo-pgrx_0_12_6
    cargo-pgrx_0_16_0
    cargo-pgrx_0_16_1
    cargo-pgrx
    ;

  buildPgrxExtension = callPackage ./pkgs/cargo-pgrx/buildPgrxExtension.nix { };

  # PGXS builder for PostgreSQL extensions. Pairs with postgresql.pg_config.
  postgresqlBuildExtension = postgresql.buildExtension;

  rust-bindgen-unwrapped = callPackage ./pkgs/rust-bindgen/unwrapped.nix { };

  mkNugetDeps = null; # TODO(corepkgs): implement NuGet dependency fetcher
  mkNugetSource = null; # TODO(corepkgs): implement NuGet source builder

  buildFHSEnv = buildFHSEnvBubblewrap;

  uboot = callFromScope ./pkgs/uboot { };
  inherit (uboot) buildUBoot;

  inherit (arm-trusted-firmware) buildArmTrustedFirmware;
}
