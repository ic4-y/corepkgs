{
  lib,
  stdenv,
  fetchFromGitHub,
  fetchpatch,
  cmake,
  zlib,
  c-ares,
  pkg-config,
  re2,
  openssl,
  protobuf,
  abseil-cpp,
  libnsl,
}:

stdenv.mkDerivation (finalAttrs: {
  pname = "grpc";
  version = "1.84.0";

  outputs = [
    "out"
    "include"
  ];
  outputInclude = "include";

  src = fetchFromGitHub {
    owner = "grpc";
    repo = "grpc";
    tag = "v${finalAttrs.version}";
    hash = "sha256-cuNa/Juh8/whghdtbSaEDMqGRASt6vbzeEtHp34STrc=";
    fetchSubmodules = true;
  };

  patches = [
    (fetchpatch {
      # armv6l support, https://github.com/grpc/grpc/pull/21341
      name = "grpc-link-libatomic.patch";
      url = "https://github.com/lopsided98/grpc/commit/a9b917666234f5665c347123d699055d8c2537b2.patch";
      hash = "sha256-Lm0GQsz/UjBbXXEE14lT0dcRzVmCKycrlrdBJj+KLu8=";
    })
  ];

  nativeBuildInputs = [
    cmake
    cmake.configurePhaseHook
    pkg-config
  ];

  propagatedBuildInputs = [
    c-ares
    re2
    zlib
    abseil-cpp
  ];

  buildInputs = [
    openssl
    protobuf
  ]
  ++ lib.optionals stdenv.hostPlatform.isLinux [
    libnsl
  ];

  cmakeEntries = {
    gRPC_ZLIB_PROVIDER = "package";
    gRPC_CARES_PROVIDER = "package";
    gRPC_RE2_PROVIDER = "package";
    gRPC_SSL_PROVIDER = "package";
    gRPC_PROTOBUF_PROVIDER = "package";
    gRPC_ABSL_PROVIDER = "package";
    BUILD_SHARED_LIBS = true;
  };

  cmakeBuildType = "Release";

  # CMake creates a build directory by default, this conflicts with the
  # bazel BUILD file on case-insensitive filesystems.
  preConfigure = ''
    rm -vf BUILD
  '';

  # When natively compiling, grpc_cpp_plugin is executed from the build directory,
  # needing to load dynamic libraries from the build directory.
  preBuild = ''
    export LD_LIBRARY_PATH=$(pwd)''${LD_LIBRARY_PATH:+:}$LD_LIBRARY_PATH
  '';

  env.NIX_CFLAGS_COMPILE = toString [
    "-Wno-error"
  ];

  meta = {
    description = "C based gRPC (C++, Python, Ruby, Objective-C, PHP, C#)";
    license = lib.licenses.asl20;
    homepage = "https://grpc.io/";
    platforms = lib.platforms.all;
    changelog = "https://github.com/grpc/grpc/releases/tag/v${finalAttrs.version}";
    identifiers.cpeParts = lib.meta.cpeFullVersionWithVendor "grpc" finalAttrs.version;
  };
})
